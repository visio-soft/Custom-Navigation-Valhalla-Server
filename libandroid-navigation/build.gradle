apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'kotlinx-serialization'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

kotlin {
    jvmToolchain(17)
}

android {
    namespace 'org.maplibre.navigation.android.navigation'

    compileSdkVersion androidVersions.compileSdkVersion
    buildToolsVersion androidVersions.buildToolsVersion

    defaultConfig {
        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField('String',
                "MAPLIBRE_NAVIGATION_SDK_IDENTIFIER", String.format("\"%s\"", "maplibre-navigation-android")
        )
        buildConfigField('String',
                "MAPLIBRE_NAVIGATION_VERSION_NAME", String.format("\"%s\"", project.VERSION_NAME)
        )
        buildConfigField "String",
                "MAPLIBRE_NAVIGATION_EVENTS_USER_AGENT", String.format("\"maplibre-navigation-android/%s\"",
                project.VERSION_NAME
        )
        consumerProguardFiles 'proguard-consumer.pro'
    }

    configurations {
        javadocDeps
    }

    buildTypes {
        release {
            debuggable true
        }
        debug {
            testCoverageEnabled = true
        }
    }

    buildFeatures {
        buildConfig true
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    api dependenciesList.mapLibreTurf
    implementation dependenciesList.mapLibre

    implementation dependenciesList.androidxCore
    implementation dependenciesList.kotlinstdlib

    compileOnly dependenciesList.gmsLocation

    // Support
    implementation dependenciesList.androidxAppcompat
    implementation dependenciesList.supportAnnotation

    // Logging
    implementation dependenciesList.timber

    implementation dependenciesList.kotlinxSerializationJson
    
    // Unit testing
    testImplementation dependenciesList.junit
    testImplementation dependenciesList.hamcrest
    testImplementation dependenciesList.commonsIO
    testImplementation dependenciesList.robolectric
    testImplementation dependenciesList.androidxTestKtx
    testImplementation dependenciesList.mockk
}

apply from: 'javadoc.gradle'
apply from: "${rootDir}/gradle/mvn-push-android.gradle"
apply from: "${rootDir}/gradle/checkstyle.gradle"
apply from: "${rootDir}/gradle/dependencies-graph.gradle"
apply from: "${rootDir}/gradle/dependency-updates.gradle"

